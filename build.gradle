buildscript {
	ext {
		springBootVersion = '2.1.1.RELEASE'
	}
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath('org.asciidoctor:asciidoctor-gradle-plugin:1.5.8')
	}
}


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.asciidoctor.convert'


group = 'com.CurriculumProject'
version = '1.0.6'
sourceCompatibility = 1.8

configurations {
    jsondoclet
}

ext {
    snippetsDir = file('build/generated-snippets')
    javadocJsonDir = file("$buildDir/generated-javadoc-json")
}

repositories {
	mavenCentral()
	maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
	jcenter()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-web')
	runtime('org.springframework.boot:spring-boot-devtools')
	runtime('org.postgresql:postgresql')
	testCompile ('org.springframework.restdocs:spring-restdocs-mockmvc:2.0.1.RELEASE')
	testCompile ('org.springframework.boot:spring-boot-starter-test')
	asciidoctor('org.springframework.restdocs:spring-restdocs-asciidoctor')
	compile group: 'capital.scalable', name: 'spring-auto-restdocs-core', version: '2.0.2'
	compile group: 'capital.scalable', name: 'spring-auto-restdocs-json-doclet', version: '2.0.2'
	testCompile group: 'capital.scalable', name: 'spring-auto-restdocs-core', version: '2.0.3-SNAPSHOT' 
    jsondoclet group: 'capital.scalable', name: 'spring-auto-restdocs-json-doclet', version: '2.0.3-SNAPSHOT'
}

task jsonDoclet(type: Javadoc, dependsOn: compileJava) {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
    destinationDir = javadocJsonDir 
    options.docletpath = configurations.jsondoclet.files.asType(List)
    options.doclet = 'capital.scalable.restdocs.jsondoclet.ExtractDocumentationAsJsonDoclet'
    options.memberLevel = JavadocMemberLevel.PACKAGE
}

test {
	outputs.dir snippetsDir
    systemProperty 'org.springframework.restdocs.javadocJsonDir', javadocJsonDir 

    dependsOn jsonDoclet
}



asciidoctor {
	dependsOn test
	attributes 'snippets': snippetsDir
  	sourceDir = file('src/main/asciidoc')
  	sources {
    	include 'index.adoc'
  	}
  outputDir = file('build/docs')
}

bootJar {
	dependsOn asciidoctor 
	from ("${asciidoctor.outputDir}/html5") { 
		into 'static/docs'
	}
}

jar {
    dependsOn asciidoctor
}